plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "net.kyori.indra.licenser.spotless" version "3.1.3"
}

group "dev.architectury"

def runNumber = System.getenv("GITHUB_RUN_NUMBER") ?: "9999"

version = base_version + "." + runNumber

logger.lifecycle(":building architectury loom runtime v${version}")

java {
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 8
}

repositories {
    maven { url "https://maven.architectury.dev/" }
    maven { url "https://maven.fabricmc.net/" }
    maven {
        url "https://maven.minecraftforge.net/"
        metadataSources {
            artifact()
        }
    }
    mavenCentral()
}

configurations {
    include
    compileOnly.extendsFrom include
}

indraSpotlessLicenser {
    licenseHeaderFile rootProject.file("HEADER")
    newLine true
    property 'name', 'architectury'
    property 'year', '2020, 2021'
}

spotless {
    java {
        targetExclude '**/loom/forgeruntime/mixin/MixinIntermediaryDevRemapper.java'
    }
}

dependencies {
    include "net.fabricmc:tiny-mappings-parser:0.3.0+build.17"

    compileOnly "cpw.mods:modlauncher:9.0.7"
    compileOnly "org.spongepowered:mixin:0.8.2"
    compileOnly "com.google.code.gson:gson:2.8.6"
    compileOnly "com.google.guava:guava:21.0"
    compileOnly "org.apache.logging.log4j:log4j-api:2.11.2"
}

jar {
    archiveClassifier = "raw"
}

shadowJar {
    relocate "net.fabricmc.mapping", "dev.architectury.loom.forgeruntime.shadow.mapping"
    relocate "net.fabricmc.mappings", "dev.architectury.loom.forgeruntime.shadow.mappings"
    configurations = [project.configurations.include]
    archiveClassifier = ""

    manifest {
        attributes 'Automatic-Module-Name': 'dev.architectury.loom.runtime'
    }
}

assemble.dependsOn shadowJar

publishing {
    publications {
        main(MavenPublication) {
            shadow.component it
            artifact sourcesJar
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
